android {
    sourceSets {
        google {
            java.srcDirs += ['src/google/java']
            res.srcDirs += ['src/google/res']
        }
    }
}

dependencies {
    // Google version use google fonts, otherwise bundle fonts
    //noinspection UseTomlInstead
    googleImplementation libs.compose.google.fonts

    googleImplementation platform(libs.firebase.bom)
    googleImplementation libs.firebase.analytics
    googleImplementation libs.firebase.crashlytics
    googleImplementation libs.firebase.perf
    googleImplementation libs.firebase.messaging

    googleImplementation libs.play.services.location
    googleImplementation libs.okhttp
}

def taskNames = gradle.startParameter.taskNames
def isGoogleVariant = taskNames.any { it.toLowerCase().contains("google") }
def isIdeSync = System.getProperty("idea.sync.active") == "true"

if (isGoogleVariant || isIdeSync) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}
